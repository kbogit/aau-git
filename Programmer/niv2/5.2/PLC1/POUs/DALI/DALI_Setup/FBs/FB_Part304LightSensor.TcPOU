<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Part304LightSensor" Id="{5b9d6038-da63-49c9-a5da-23ee1fdc4688}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Part304LightSensor
VAR_INPUT
	ipDALICommunication			:	I_DALICommunication;
END_VAR
VAR_OUTPUT
	bBusy						:	BOOL;
END_VAR
VAR
{region "Visu"}
	nInputValue					:	LWORD;
	nInstanceError				:	BYTE;
	nResolution					:	BYTE;
	eEventScheme				:	E_DALIEventScheme;
	nEventFilter				:	DWORD;
	nReport						:	BYTE(0..255);
	nDeadtime					:	UINT(0..12750);
	nHysteresis					:	BYTE(0..25);
	nHysteresisMin				:	BYTE(0..255);
	bRead						:	BOOL;
	bWrite						:	BOOL;
	bEnable						:	BOOL;
	bIlluminanceLevel			:	BOOL;
	bNoReportTimer				:	BOOL;
	bNoDeadtimeTimer			:	BOOL;
	bNoHysteresis				:	BOOL;
	bNoHysteresisMin			:	BOOL;
	eEventPriority				:	E_DALIEventPriority := E_DALIEventPriority.MiddleLow;
{endregion}
	nStep						:	INT;
	nAddress					:	BYTE;
	nInstanceAddress			:	BYTE;
	bHideWaitSymbol				:	BOOL := TRUE;

	fb103StartQuiescentMode		:	FB_DALI103StartQuiescentMode(0);
	fb103StopQuiescentMode		:	FB_DALI103StopQuiescentMode(0);
	fb103QueryInstanceError		:	FB_DALI103QueryInstanceError(0);
	fb103QueryInstanceStatus	:	FB_DALI103QueryInstanceStatus(0);
	fb103QueryResolution		:	FB_DALI103QueryResolution(0);
	fb103QueryInputValue 		:	FB_DALI103QueryInputValue(0);
	fb103QueryEventScheme 		:	FB_DALI103QueryEventScheme(0);
	fb103QueryEventFilter 		:	FB_DALI103QueryEventFilter(0);
	fb103QueryEventPriority		:	FB_DALI103QueryEventPriority(0);
	fb304QueryReportTimer		:	FB_DALI304QueryReportTimer(0);
	fb304QueryDeadtimeTimer		:	FB_DALI304QueryDeadtimeTimer(0);
	fb304QueryHysteresis		:	FB_DALI304QueryHysteresis(0);
	fb304QueryHysteresisMin		:	FB_DALI304QueryHysteresisMin(0);
	fb103EnableInstance 		:	FB_DALI103EnableInstance(0);
	fb103DisableInstance		:	FB_DALI103DisableInstance(0);
	fb103SetEventScheme 		:	FB_DALI103SetEventScheme(0);
	fb103SetEventFilter 		:	FB_DALI103SetEventFilter(0);
	fb103SetEventPriority		:	FB_DALI103SetEventPriority(0);
	fb304SetReportTimer			:	FB_DALI304SetReportTimer(0);
	fb304SetDeadtimeTimer		:	FB_DALI304SetDeadtimeTimer(0);
	fb304SetHysteresis			:	FB_DALI304SetHysteresis(0);
	fb304SetHysteresisMin		:	FB_DALI304SetHysteresisMin(0);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[{region "Detect Commands"}
IF (bRead AND (nStep = 0)) THEN
	nStep := 5;
	bRead := FALSE;
	bHideWaitSymbol := FALSE;
END_IF

IF (bWrite AND (nStep = 0)) THEN
	nStep := 1000;
	bWrite := FALSE;
	bHideWaitSymbol := FALSE;
END_IF
{endregion}
{region "Manage Dialog Selection}
IF (nStep = 0) THEN
	bBusy := FALSE;
ELSE
	bBusy := TRUE;
END_IF
{endregion}
{region "Execute Commands"}
CASE nStep OF
0:
{region "Init"}
	bRead := FALSE;
	bWrite := FALSE;
	bHideWaitSymbol := TRUE;

	fb103StartQuiescentMode.ipDALICommunication := ipDALICommunication;
	fb103StopQuiescentMode.ipDALICommunication := ipDALICommunication;
	fb103QueryInstanceError.ipDALICommunication := ipDALICommunication;
	fb103QueryInstanceStatus.ipDALICommunication := ipDALICommunication;
	fb103QueryResolution.ipDALICommunication := ipDALICommunication;
	fb103QueryInputValue.ipDALICommunication := ipDALICommunication;
	fb103QueryEventScheme.ipDALICommunication := ipDALICommunication;
	fb103QueryEventFilter.ipDALICommunication := ipDALICommunication;
	fb103QueryEventPriority.ipDALICommunication := ipDALICommunication;
	fb304QueryReportTimer.ipDALICommunication := ipDALICommunication;
	fb304QueryDeadtimeTimer.ipDALICommunication := ipDALICommunication;
	fb304QueryHysteresis.ipDALICommunication := ipDALICommunication;
	fb304QueryHysteresisMin.ipDALICommunication := ipDALICommunication;
	fb103EnableInstance.ipDALICommunication := ipDALICommunication;
	fb103DisableInstance.ipDALICommunication := ipDALICommunication;
	fb103SetEventScheme.ipDALICommunication := ipDALICommunication;
	fb103SetEventFilter.ipDALICommunication := ipDALICommunication;
	fb103SetEventPriority.ipDALICommunication := ipDALICommunication;
	fb304SetReportTimer.ipDALICommunication := ipDALICommunication;
	fb304SetDeadtimeTimer.ipDALICommunication := ipDALICommunication;
	fb304SetHysteresis.ipDALICommunication := ipDALICommunication;
	fb304SetHysteresisMin.ipDALICommunication := ipDALICommunication;
{endregion}

{region "Read"}
5:
{region "Start Qiescent Mode"}
	fb103StartQuiescentMode(bStart := TRUE,
							eAddressType := Tc3_DALI.E_DALIAddressType.Broadcast,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle);
	IF (NOT fb103StartQuiescentMode.bBusy) THEN
		fb103StartQuiescentMode(bStart := FALSE);
		nStep := 10;
	END_IF
{endregion}
10:
{region "Query Instance Error"}
	fb103QueryInstanceError(bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nInstanceError => nInstanceError);
	IF (NOT fb103QueryInstanceError.bBusy) THEN
		fb103QueryInstanceError(bStart := FALSE);
		IF (NOT fb103QueryInstanceError.bError) THEN
			nStep := 20;
		ELSE
			nStep := 0;
		END_IF
	END_IF
{endregion}
20:
{region "Query Instance Status"}
	fb103QueryInstanceStatus(	bStart := TRUE,
								nAddress := nAddress,
								eAddressType := Tc3_DALI.E_DALIAddressType.Short,
								nInstanceAddress := nInstanceAddress,
								eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
								eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle);
	IF (NOT fb103QueryInstanceStatus.bBusy) THEN
		fb103QueryInstanceStatus(bStart := FALSE);
		IF (fb103QueryInstanceStatus.nInstanceStatus.1) THEN
			bEnable := TRUE;
		END_IF
		nStep := 30;
	END_IF
{endregion}
30:
{region "Query Resolution"}
	fb103QueryResolution(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nResolution => nResolution);
	IF (NOT fb103QueryResolution.bBusy) THEN
		fb103QueryResolution(bStart := FALSE);
		nStep := 40;
		IF (fb103QueryResolution.ipResultMessage.nEventId = TC_EVENTS.TcDALIEventClass.NoResponseFromTheDALIDeviceInstance.nEventId) THEN
			nStep := 0;
		END_IF
	END_IF
{endregion}
40:
{region "Query Input Value"}
	fb103QueryInputValue(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nResolution := nResolution,
							nInputValue => nInputValue);
	IF (NOT fb103QueryInputValue.bBusy) THEN
		fb103QueryInputValue(bStart := FALSE);
		nStep := 50;
	END_IF
{endregion}
50:
{region "Query Event Scheme"}
	fb103QueryEventScheme(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							eEventScheme => eEventScheme);
	IF (NOT fb103QueryEventScheme.bBusy) THEN
		fb103QueryEventScheme(bStart := FALSE);
		nStep := 60;
	END_IF
{endregion}
60:
{region "Query Event Filter"}
	fb103QueryEventFilter(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nEventFilter => nEventFilter);
	IF (NOT fb103QueryEventFilter.bBusy) THEN
		fb103QueryEventFilter(bStart := FALSE);
		bIlluminanceLevel := nEventFilter.0;
		nStep := 70;
	END_IF
{endregion}
70:
{region "Query Report Timer"}
	fb304QueryReportTimer(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nReport => nReport);
	IF (NOT fb304QueryReportTimer.bBusy) THEN
		fb304QueryReportTimer(bStart := FALSE);
		nStep := 80;
	END_IF
{endregion}
80:
{region "Query Deadtime Timer"}
	fb304QueryDeadtimeTimer(bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle);
	nDeadtime := (	fb304QueryDeadtimeTimer.nDeadtime * 50);
	IF (NOT fb304QueryDeadtimeTimer.bBusy) THEN
		fb304QueryDeadtimeTimer(bStart := FALSE);
		nStep := 90;
	END_IF
{endregion}
90:
{region "Query Hysteresis"}
	fb304QueryHysteresis(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nHysteresis => nHysteresis);
	IF (NOT fb304QueryHysteresis.bBusy) THEN
		fb304QueryHysteresis(bStart := FALSE);
		nStep := 100;
	END_IF
{endregion}
100:
{region "Query Hysteresis Min"}
	fb304QueryHysteresisMin(bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nHysteresisMin => nHysteresisMin);
	IF (NOT fb304QueryHysteresisMin.bBusy) THEN
		fb304QueryHysteresisMin(bStart := FALSE);
		nStep := 110;
	END_IF
{endregion}
110:
{region "Query Event Priority"}
	fb103QueryEventPriority(bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							eEventPriority => eEventPriority);
	IF (NOT fb103QueryEventPriority.bBusy) THEN
		fb103QueryEventPriority(bStart := FALSE);
		nStep := 120;
	END_IF
{endregion}
120:
{region "Stop Qiescent Mode"}
	fb103StopQuiescentMode(	bStart := TRUE,
							eAddressType := Tc3_DALI.E_DALIAddressType.Broadcast,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle);
	IF (NOT fb103StopQuiescentMode.bBusy) THEN
		fb103StopQuiescentMode(	bStart := FALSE);
		nStep := 0;
	END_IF
{endregion}
{region "Write"}
1000:
{region "Enable Instance"}
	IF (bEnable) THEN
		fb103EnableInstance(bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle);
		IF (NOT fb103EnableInstance.bBusy) THEN
			fb103EnableInstance(bStart := FALSE);
			IF (NOT fb103EnableInstance.bError) THEN
				nStep := 1010;
			ELSE
				nStep := 0;
			END_IF
		END_IF
	ELSE
		fb103DisableInstance(	bStart := TRUE,
								nAddress := nAddress,
								eAddressType := Tc3_DALI.E_DALIAddressType.Short,
								nInstanceAddress := nInstanceAddress,
								eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
								eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle);
		IF (NOT fb103DisableInstance.bBusy) THEN
			fb103DisableInstance(bStart := FALSE);
			nStep := 0;
		END_IF
	END_IF
{endregion}
1010:
{region "Set Event Scheme"}
	fb103SetEventScheme(bStart := TRUE,
						nAddress := nAddress,
						eAddressType := Tc3_DALI.E_DALIAddressType.Short,
						nInstanceAddress := nInstanceAddress,
						eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
						eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
						eEventScheme := eEventScheme);
	IF (NOT fb103SetEventScheme.bBusy) THEN
		fb103SetEventScheme(bStart := FALSE);
		IF (NOT fb103SetEventScheme.bError) THEN
			nStep := 1020;
		ELSE
			nStep := 0;
		END_IF
	END_IF
{endregion}
1020:
{region "Set Event Filter"}
	nEventFilter.0 := bIlluminanceLevel;
	fb103SetEventFilter(bStart := TRUE,
						nAddress := nAddress,
						eAddressType := Tc3_DALI.E_DALIAddressType.Short,
						nInstanceAddress := nInstanceAddress,
						eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
						eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
						nEventFilter := nEventFilter);
	IF (NOT fb103SetEventFilter.bBusy) THEN
		fb103SetEventFilter(bStart := FALSE);
		IF (NOT fb103SetEventFilter.bError) THEN
			nStep := 1030;
		END_IF
	END_IF
{endregion}
1030:
{region "Set Report Timer"}
	IF (bNoReportTimer) THEN
		nStep := 1040;
	ELSE
		fb304SetReportTimer(bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nReport := nReport);
		IF (NOT fb304SetReportTimer.bBusy) THEN
			fb304SetReportTimer(bStart := FALSE);
			IF (NOT fb304SetReportTimer.bError) THEN
				nStep := 1040;
			ELSE
				nStep := 0;
			END_IF
		END_IF
	END_IF
{endregion}
1040:
{region "Set Deadtime Timer"}
	IF (bNoDeadtimeTimer) THEN
		nStep := 1050;
	ELSE
		fb304SetDeadtimeTimer(	bStart := TRUE,
								nAddress := nAddress,
								eAddressType := Tc3_DALI.E_DALIAddressType.Short,
								nInstanceAddress := nInstanceAddress,
								eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
								eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
								nDeadtime := TO_BYTE( nDeadtime / 50));
		IF (NOT fb304SetDeadtimeTimer.bBusy) THEN
			fb304SetDeadtimeTimer(bStart := FALSE);
			IF (NOT fb304SetDeadtimeTimer.bError) THEN
				nStep := 1050;
			ELSE
				nStep := 0;
			END_IF
		END_IF
	END_IF
{endregion}
1050:
{region "Set Hysteresis"}
	IF (bNoHysteresis) THEN
		nStep := 1060;
	ELSE
		fb304SetHysteresis(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							nHysteresis := nHysteresis);
		IF (NOT fb304SetHysteresis.bBusy) THEN
			fb304SetHysteresis(bStart := FALSE);
			IF (NOT fb304SetHysteresis.bError) THEN
				nStep := 1060;
			ELSE
				nStep := 0;
			END_IF
		END_IF
	END_IF
{endregion}
1060:
{region "Set Hysteresis Min"}
	IF (bNoHysteresisMin) THEN
		nStep := 1070;
	ELSE
		fb304SetHysteresisMin(	bStart := TRUE,
								nAddress := nAddress,
								eAddressType := Tc3_DALI.E_DALIAddressType.Short,
								nInstanceAddress := nInstanceAddress,
								eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
								eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
								nHysteresisMin := nHysteresisMin);
		IF (NOT fb304SetHysteresisMin.bBusy) THEN
			fb304SetHysteresisMin(bStart := FALSE);
			nStep := 1070;
		END_IF
	END_IF
1070:
{region "Set Event Priority"}
	fb103SetEventPriority(	bStart := TRUE,
							nAddress := nAddress,
							eAddressType := Tc3_DALI.E_DALIAddressType.Short,
							nInstanceAddress := nInstanceAddress,
							eInstanceAddressType := Tc3_DALI.E_DALIInstanceAddressType.InstanceNumber,
							eCommandPriority := Tc3_DALI.E_DALICommandPriority.Middle,
							eEventPriority:= eEventPriority);
	IF (NOT fb103SetEventPriority.bBusy) THEN
		fb103SetEventPriority(bStart := FALSE);
		nStep := 0;
	END_IF
{endregion}
END_CASE
{endregion}
{endregion}]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>